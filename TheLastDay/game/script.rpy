# Вы можете расположить сценарий своей игры в этом файле.

# Определение персонажей игры.
define a = Character("Анастасия", color="#c9a0dc")
define o = Character("Ольга", color="#a0c9dc")
define t = Character("Татьяна", color="#dca0a0")
define m = Character("Мария", color="#a0dca0")
define n = Character("Николай", color="#dcdca0")
define al = Character("Александра", color="#dcb5a0")
define ax = Character("Алексей", color="#b5a0dc")
define g = Character("Охранник", color="#808080")
define b = Character("Доктор Боткин", color="#a0dcdc")


# В самом начале игры
label start:
    # Сохраняем оригинальный overlay
    $ original_overlay = config.overlay_screens.copy()
    $ original_quick_menu = quick_menu
    
    # ПОЛНОЕ отключение всего интерфейса
    $ config.overlay_screens = []
    $ _preferences.afm_enable = False
    $ quick_menu = False
    
    # Скрываем все окна и кнопки
    window hide
    
    scene black with fade
    
    $ intro_texts = [
        "16 июля 1918 года.",
        "Екатеринбург. Дом Ипатьева.",
        "Семья Романовых находится в заточении уже 78 дней."
    ]
    
    $ i = 0
    while i < len(intro_texts):
        $ text_line = intro_texts[i]
        
        # Используем centered для полного отключения интерфейса
        centered "{color=#ffffff}[text_line]{/color}"
        
        $ i += 1
    
    # Восстанавливаем интерфейс
    $ quick_menu = original_quick_menu
    
    jump morning


# Игра начинается здесь:
label morning:

    # Включаем интерфейс только сейчас
    $ config.overlay_screens = original_overlay
    window show

    
    python:
        if persistent.inventory_data:
            inventory = persistent.inventory_data
        else:
            inventory = Inventory()

    scene bg living with fade

    "Комната девушек. Серый свет сочится сквозь забеленные окна."
    "На походных кроватях спят Ольга, Татьяна, Мария и Анастасия."

    show nic smile

    "Внутренний голос Анастасии" "Сегодня мне снился Тобольск."

    "Внутренний голос Анастасии" "Как мы гуляли по берегу Иртыша..."

    "Внутренний голос Анастасии" "Папа пилил дрова, а мы с Алексеем лепили снежную бабу."

    "Внутренний голос Анастасии" "Странно...Почему мне снится снег? Ведь сейчас июль..."

    menu:
        "Осмотреться в комнате":
            jump examine_room_morning
        "Разбудить сестер":
            jump wake_sisters



label examine_room_morning:
    scene bg girls_room_detail with dissolve
    
    "Анастасия оглядывает комнату. Всё как обычно, но сегодня почему-то хочется рассмотреть каждую мелочь."
    
    # Меню осмотра с тремя вариантами
    menu:
        "Осмотреть тумбочку":
            $ inventory.add_item(1)
            "На тумбочке лежит небольшая икона Спаса Нерукотворного. Анастасия берет её в руки, крестится."
            "Внутренний голос Анастасии" "Эту икону мама дала мне перед отъездом из Царского Села. Как давно это было... Целая вечность назад."
            "Анастасия прячет икону под подушку."
        
        "Посмотреть в окно":
            "Анастасия подходит к окну. Сквозь забеленные стекла почти ничего не видно — только размытые силуэты деревьев и кусочек неба."
            "Внутренний голос Анастасии" "Раньше в Царском Селе я могла часами смотреть в окно. Там был парк, пруд, лебеди... А здесь — только белая краска."
            "Она проводит пальцем по стеклу, но тут же отдергивает руку — во дворе слышны шаги охраны."
            "Внутренний голос Анастасии" "Они всегда ходят. Днем и ночью. Смотрят на окна. Иногда мне кажется, что они ждут чего-то."
        
        "Посмотреть на спящих сестер":
            "Анастасия обводит взглядом спящих сестер."
            "Ольга спит беспокойно — нахмурила брови, сжала губы. Даже во сне не может расслабиться, всегда переживает за всех."
            "Внутренний голос Анастасия" "Оля как вторая мама. Только мама устала, а Оля держится. Ради нас."
            "Татьяна поджала ноги к груди, свернулась калачиком. Даже во сне изящная, аккуратная."
            "Внутренний голос Анастасия" "Таня всегда говорила, что так теплее. Наверное, ей холодно. Нам всем холодно последнее время."
            "Мария улыбается во сне, чему-то своему, девичьему."
            "Внутренний голос Анастасии" "Машенька даже здесь находит повод для улыбки. Говорит, что Бог не оставляет, солнышко светит и птички поют. Хорошо, что она у нас есть."
            "Анастасия задерживает взгляд на каждой сестре и чувствует, как к горлу подступает комок."
            "Внутренний голос Анастасии" "Лишь бы мы были вместе. Что бы ни случилось — лишь бы вместе."
        
        "Вернуться к сестрам":
            jump wake_sisters
    
    # После любого осмотра возвращаемся к пробуждению сестер
    jump wake_sisters

label wake_sisters:

    scene bg girls_room with dissolve
    
    "Анастасия тихо окликает сестер."
    
    show olga sleepy at left
    show anastasia sitting at center
    
    o "Настя? Ты чего не спишь? Еще рано."
    
    a "Не знаю... Показалось, кто-то позвал."
    
    o "Это, наверное, Джой скулил во дворе. Слышала, как охрана ночью ходила?"
    
    menu:
        "«Опять обыски?»":
            a "Опять обыски? Вчера же уже были..."
            o "Не знаю. Но шаги были тяжелые, солдатские. Не меньше трех человек."
            a "Интересно, что они ищут каждую ночь? У нас же ничего нет."
            
            if inventory.has_item(1):
                "Анастасия невольно касается подушки, под которой спрятана икона."
                o "Ты что-то нашла, Настя?"
                a "Икону. Ту, что мама дала. Лежала на тумбочке."
                o "Храни её. Это теперь наше единственное богатство."
            else:
                o "Тише, Настя. Разбудишь Марию, она потом весь день ходит сонная."
        
        "«Я ничего не слышала»":
            a "Я ничего не слышала. Спала крепко."
            o "Счастливая. Я уже вторую ночь просыпаюсь от каждого шороха."
            a "Оля... Тебе страшно?"
            o "..."
            o "Нет. Просто непривычно. В Царском было так тихо ночью."
            
            # Дополнительный диалог в зависимости от выбора осмотра
            if inventory.has_item(1):
                a "Я сегодня икону нашла. Ту, что мама давала. Будто почувствовала что-то."
                o "Знаешь, Настя... Мне иногда кажется, что иконы нас хранят."
            else:
                a "А мне сегодня Тобольск снился. Снег, папа дрова пилит... Странно, правда?"
                o "Наверное, ты скучаешь по тем дням. Мы все скучаем."
    
    # Пробуждение других сестер (автоматически, если не осматривали, или после осмотра)
    show tatiana sleepy with dissolve
    show maria sleepy with dissolve
    
    t "Вы чего шепчетесь? Еще ночь же..."
    
    m "А который час?"
    
    o "Уже утро. Скоро придут за нами на умывание."
    
    # Финальные мысли Анастасии
    "Анастасия смотрит на сестер и чувствует тепло в груди."
    
    if inventory.count_found() >= 1:
        "Внутренний голос Анастасии" "Икона со мной. Сестры рядом. Мама с папой за стеной. Пока мы вместе — ничего не страшно."
    else:
        "Внутренний голос Анастасии" "Мы всё выдержим. Потому что мы вместе. Потому что мы — семья."
    
    # Переход к следующей сцене
    scene black with fade
    "Через некоторое время в дверь постучат — охрана зовет умываться."
    
    jump scene_washing

label scene_washing:
    scene bg corridor_ipatiev with dissolve
    "Узкий коридор дома Ипатьева. Сестры идут к уборной в сопровождении красноармейца."
    show guard rude at right
    show anastasia at left
    show olga 
    show tatiana 
    show maria
    
    g "Шевелитесь, барышни. Нечего тут прохлаждаться."
    
    if inventory.has_item(1):
        "Внутренний голос Анастасии" "Грубит, как всегда. Но сегодня я почему-то не злюсь."
    

    "Мария наклоняется к сестрам и говорит едва слышно."
    
    m "Этот вчера у Тани спросил, не скучно ли нам без балов."
    
    "Татьяна бледнеет, но отвечает шепотом."
    
    t "Я сделала вид, что не слышу. Мама велела не отвечать на грубости."
    
    o "Главное, чтобы Алексея не тревожили. У него ночью нога болела. Я слышала, он стонал."
    
    "Анастасия вспоминает, что вчера видела что-то странное."
    
    a "Я слышала, Леню Седнева куда-то увели вчера."
    
    m "Этот поваренок... Жаль, ведь они с Лешенькой дружили."
    
    t "Да. Мама тоже тревожится, увидим ли мы вновь этого мальчика."
        
    if inventory.count_found() >= 1:
        menu:
            "Поделиться находкой с сестрами":
                a "Девочки... Я сегодня иконку нашла. На тумбочке лежала. Та, спасовая."
                o "Тише, Настя! Охранник услышит."
                o "Спрячь получше. Это теперь наша святыня."
                t "Мама говорила, что иконы нас хранят. Может, это знак?"
                m "Конечно, знак! Бог с нами, девочки. Он не оставит."
                $ persistent.sister_bond = True
                
            "Промолчать о находке":
                "Внутренний голос Анастасии" "Слишком опасно — охрана может услышать. Расскажу вечером, когда останемся одни."
    
    # Подходим к уборной
    scene bg toilet_door with dissolve
    
    g "Заходите по одной. Остальные ждите здесь."
    
    "Первой заходит Ольга. Сестры ждут в коридоре под присмотром охраны."
    
    # Мини-игра "Тихий разговор" (выбор действий, пока ждем)
    
    menu:
        "Переглянуться с Марией":
            "Анастасия ловит взгляд Марии. Та улыбается — той самой своей светлой улыбкой, которая всегда согревает."
            m (тихо) "Помнишь, как в Царском мы бегали к пруду купаться? А мама потом ругалась, что мы мокрые?"
            a (тихо) "Помню. Ты тогда в платье в воду прыгнула, потому что жарко было."
            m (тихо) "А Татьяна нас прикрывала, говорила, что мы просто облились."
            "Сестры тихо смеются, но тут же замолкают — охранник оборачивается."
            "Внутренний голос Анастасии" "Хорошо, что есть Мария. Она умеет напомнить, что мы просто девочки, а не узницы."
            
        "Посмотреть на охранника":
            "Анастасия внимательно смотрит на красноармейца. Ей кажется, что сегодня он ведет себя иначе — более нервно, часто оглядывается, теребит винтовку."
            "Внутренний голос Анастасии" "Он будто ждет чего-то. Или боится. Интересно, о чем они думают, эти солдаты? Снятся ли им те, кого они стерегут?"
            "Охранник ловит взгляд Анастасии"
            g "Чего уставилась? Отвернись."
            "Анастасия отводит глаза, но чувство тревоги не проходит."
            
            if inventory.has_item(1):
                "Анастасия машинально сжимает в кармане икону."
                "Внутренний голос Анастасии" "Господи, сохрани нас. Что бы ни случилось — сохрани."
    
    show olga at left with dissolve
    
    o "Я закончила. Таня, иди ты."
    
    "Татьяна заходит в уборную. Ожидание продолжается."
    
    # Второй раунд мини-выборов
    menu:
        "Спросить Ольгу о самочувствии Алексея":
            a "Оля, ты говорила, у Алексея нога болела. Он сильно мучился?"
            o "Я слышала, как он плакал ночью. Тихо так, в подушку. Чтобы мама не слышала."
            a "Бедный Лешка. Он же совсем мальчик еще, а терпит как взрослый."
            o "Папа говорит, казаки так терпят. А Леша хочет быть казаком."
            "Ольга грустно улыбается."
            
        "Спросить Ольгу, что думает о словах охранника про балы":
            a "Оля, а тот охранник... про балы спросил. Это же издевательство, да?"
            o "Конечно. Но мы не должны показывать, что нам больно. Мама права — молчание лучшее оружие."
            o "Пусть думают, что мы не понимаем, не чувствуем. Мы-то знаем правду."
            
        "Просто стоять молча":
            "Анастасия молчит, думая о своем. Сестры рядом, но каждая погружена в свои мысли."
            "Внутренний голос Анастасии" "Сколько еще это будет продолжаться? Месяц? Год? Всегда?"
    
    # Выходит Татьяна, заходит Мария
    show tatiana at left behind olga with dissolve
    hide maria
    
    "Мария заходит в уборную. Татьяна присоединяется к сестрам."
    
    # Диалог с Татьяной
    t "Вода ледяная. Но я быстро."
    
    o "Таня, ты бледная. Не заболела?"
    
    t "Нет. Просто не выспалась. Думала о Лене Седневе всю ночь."
    a "Таня, не переживай. Может, его правда к дяде отпустили?"
    t "А если нет, Настя? Если с ним что-то случилось? Он же совсем ребенок."
    o "Татьяна, не накручивай себя. Мама сказала — будем надеяться на лучшее."
    t "Легко сказать... Я помню его глаза, когда его уводили. Он будто знал что-то."
            
    # Выходит Мария
    show maria at left behind tatiana with dissolve
    
    m "Фух, вода холоднющая! Я аж подпрыгнула."
    
    g "Все вышли? Тогда назад. Живо."
    
    "Сестры возвращаются по коридору."
    
    
    # Переход к завтраку
    scene black with fade

    jump scene_breakfast